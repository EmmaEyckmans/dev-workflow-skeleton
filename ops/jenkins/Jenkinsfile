stage concurrency: 1, name: "commit"
	node("linux && jdk8 && mvn3 && docker") {
	    clearWorkspace()
	    cloneProject()
	    determineVersionNumber()
	    setMvnVersion()
		buildWithoutE2ETests()
		stopWhenFailures()
		stashWorkspace()
	}

stage concurrency: 1, name: "test"
	node("linux && jdk8 && mvn3 && docker && xvfb") {
		clearWorkspace()
		unstashWorkspace()
		buildDockerImages()
		e2eTests()
		stopWhenFailures()
		tag()
	}

stage name: "pre-acc"
	input "Do you want to deploy to acc?"

stage concurrency: 1, name: "acc"
	node("linux && mvn3 && docker") {
		stopAcc()
		startAcc()
	}

stage name: "pre-prd"
	input "Do you want to deploy to prd?"

stage concurrency: 1, name: "prd"
	node("linux && mvn3 && docker") {
		stopPrd()
		startPrd()
	}

/********** Helper functions **********/
def clearWorkspace() {
	sh "rm -rf *"
}

def cloneProject() {
	git credentialsId: "385df167-99f7-408d-b9d8-ad982a7c997b", url: "https://github.com/cegeka/dev-workflow-skeleton.git"
}

def determineVersionNumber() {
	sh "git rev-list --count HEAD > nb-commits.txt"
}

def versionNumber() {
	def nbCommits = readFile("nb-commits.txt").trim()
	return "0.0.${nbCommits}"
}

def setMvnVersion() {
	mvn("versions:set -DnewVersion=${versionNumber()} -DgenerateBackupPoms=false")
}

def buildWithoutE2ETests() {
	def dbName = "dws_db_jenkins_commit"

	try {
		buildDbImage()
		startDb(dbName, "dws-jenkins")
		mvn("clean install -Ddb.host=${dbName}:5432 -pl '!:dws-acc-tests'")
	} catch(err) {
		currentBuild.result  = "FAILURE"
	} finally {
		stopDb(dbName)
		archiveAllTestResults()
		archiveArtifacts()
	}
}

def buildDockerImages() {
	buildDbImage()
	buildBackendImage()
	buildFrontendImage()
}

def buildDbImage() {
	sh "docker build -t dws/db:${versionNumber()} ops/dws/db/"
}

def buildBackendImage() {
	def versionNumber = versionNumber()
	sh "mkdir ops/dws/back-end/dws-jar"
	sh "cp dws/dws-rest/target/dws-rest-${versionNumber}.jar ops/dws/back-end/dws-jar/dws-rest.jar"
	dir("ops/dws/back-end") {
		sh "docker build -t dws/back-end:${versionNumber} ."
	}
}

def buildFrontendImage() {
	def versionNumber = versionNumber()
	sh "mkdir ops/dws/front-end/dws-dist"
	sh "cp -r dws/dws-ui/target/dist/* ops/dws/front-end/dws-dist/"
	dir("ops/dws/front-end") {
		sh "docker build -t dws/front-end:${versionNumber} ."
	}
}

def e2eTests() {
	def network = "dws-jenkins"
	def dbName = "dws_db_jenkins_test"
	def backendName = "dws_back-end_jenkins_test"
	def frontendName = "dws_front-end_jenkins_test"

	try {
		startDb(dbName, network)
		initDb(dbName)
		startBackend(backendName, dbName, network)
		startFrontend(frontendName, 9090 ,backendName, network)
		runProtractor()
	} catch(err) {
		currentBuild.result  = "FAILURE"
	} finally {
		stopFrontend(frontendName)
		stopBackend(backendName)
		stopDb(dbName)
		archiveUiTestResults()
	}
}

def runProtractor() {
	wrap([$class: "Xvfb", installationName: "xvfb"]) {
		mvn("clean install -Djenkins -pl :dws-acc-tests")
	}
}

def stopAcc() {
	stopFrontend("dws_front-end_jenkins_acc")
	stopBackend("dws_back-end_jenkins_acc")
	stopDb("dws_db_jenkins_acc")
}

def startAcc() {
	def network = "dws-acc"
	def dbName = "dws_db_jenkins_acc"
	def backendName = "dws_back-end_jenkins_acc"
	def frontendName = "dws_front-end_jenkins_acc"	
		
	createNetwork(network)	
	startDb(dbName, network)
	initDb(dbName)
	startBackend(backendName, dbName, network)
	startFrontend(frontendName, 5050, backendName, network)
}

def stopPrd() {
	stopFrontend("dws_front-end_jenkins_prd")
	stopBackend("dws_back-end_jenkins_prd")
	stopDb("dws_db_jenkins_prd")
}

def startPrd() {
	def network = "dws-prd"
	def dbName = "dws_db_jenkins_prd"
	def backendName = "dws_back-end_jenkins_prd"
	def frontendName = "dws_front-end_jenkins_prd"	
		
	createNetwork(network)	
	startDb(dbName, network)
	initDb(dbName)
	startBackend(backendName, dbName, network)
	startFrontend(frontendName, 6060, backendName, network)
}

def createNetwork(network) {
	sh "docker network create -d bridge ${network}"
}

def startDb(dbName, network) {
	try { sh "docker rm -vf ${dbName}" } catch(e) {}
	sh "docker run -d --net ${network} --name ${dbName} dws/db:${versionNumber()}"
 }

def stopDb(dbName) {
	try {
		sh "docker stop ${dbName}"
		sh "docker rm -v ${dbName}"	
	} catch(err){}
}

def initDb(dbName) {
	retry(3) { mvn("process-resources flyway:clean flyway:migrate -Ddb.host=${dbName}:5432 -pl :dws-infrastructure") }
}

def startBackend(backendName, dbName, network) {
	try { sh "docker rm -vf ${backendName}" } catch(e) {}
	sh "docker run -d --net ${network} --env db.host=${dbName}:5432 --name ${backendName} dws/back-end:${versionNumber()}"
}

def stopBackend(backendName) {
	try {
		sh "docker stop ${backendName}"
		sh "docker rm -v ${backendName}"	
	} catch(err){}
}

def startFrontend(frontendName, port, backendName, network) {
	try { sh "docker rm -vf ${frontendName}" } catch(e) {}
	sh "docker run -d -p ${port}:80 --net ${network} --env dws_api_proxy=${backendName}:8080 --name ${frontendName} dws/front-end:${versionNumber()}"
}

def stopFrontend(frontendName) {
	try {
		sh "docker stop ${frontendName}"
		sh "docker rm -v ${frontendName}"	
	} catch(err){}
}

def archiveAllTestResults() {
	step([$class: "JUnitResultArchiver", testResults: "**/target/surefire-reports/TEST-*.xml"])
	step([$class: "JUnitResultArchiver", testResults: "**/target/failsafe-reports/TEST-*.xml"])
}

def archiveUiTestResults() {
	step([$class: "JUnitResultArchiver", testResults: "**/dws-acc-tests/target/surefire-reports/TEST-*.xml"])
}

def archiveArtifacts() {
 	step([$class: "ArtifactArchiver", artifacts: "**/target/*.jar", fingerprint: true])
 	step([$class: "ArtifactArchiver", artifacts: "**/dws-ui/target/dist/**/*", fingerprint: true])
}

def stopWhenFailures() {
	if (currentBuild.result != null && !currentBuild.result.equalsIgnoreCase("STABLE")) {
	    error "There are failures in the current stage. The current build will be stopped."
	}
}

def tag() {
	sh "git tag -a ${versionNumber()} -m 'Tag version ${versionNumber()}'"
	sh "git push origin ${versionNumber()}"
}

def stashWorkspace() {
	stash includes: "**/*", name: "workspace"
}

def unstashWorkspace() {
	unstash "workspace"
}

def mvn(task) {
	def javaHome = tool "jdk8"
	def mvnHome = tool "mvn3"

	dir("dws") {
		withEnv(["JAVA_HOME=${javaHome}", "PATH+MAVEN=${mvnHome}/bin"]) {
		    sh "mvn -B -Dmaven.test.failure.ignore=true ${task}"
		}
	}
}