stage concurrency: 1, name: "commit"
	node("linux && jdk8 && mvn3 && docker") {
		def javaHome = tool "jdk8"
    	def mvnHome = tool "mvn3"
	    
	    clearWorkspace()
	    cloneProject()
		buildWithoutUiTests(javaHome, mvnHome)
		stopWhenFailures()
	}

stage concurrency: 1, name: "test"
	node("linux && jdk8 && mvn3 && docker") {
		sh "echo 'reached test stage'"
	}

/********** Helper functions **********/
def clearWorkspace() {
	sh "rm -rf *"	
}

def cloneProject() {
	git credentialsId: "385df167-99f7-408d-b9d8-ad982a7c997b", url: "https://github.com/cegeka/dev-workflow-skeleton.git"
}

def buildWithoutUiTests(javaHome, mvnHome) {
	def dbName = "dws_db_jenkins"
	try {
		startDb(dbName)
		mavenBuildWithoutUiTests(javaHome, mvnHome)
	} catch(err) {
		currentBuild.result  = "FAILURE"
	} finally {
		stopDb(dbName)
		archiveTestResults()
		archiveArtifacts()
	}
}

def startDb(dbName) {
	sh "docker build -t dws_db ops/dws/db/"
	try { sh "docker rm -f ${dbName}" } catch(e) {}
	sh "docker run -d --net jenkins --name ${dbName} dws_db"
}

def stopDb(dbName) {
	sh "docker stop ${dbName}"
	sh "docker rm ${dbName}"	
}

def mavenBuildWithoutUiTests(javaHome, mvnHome) {	
	dir("dws") {
		withEnv(["JAVA_HOME=${javaHome}", "PATH+MAVEN=${mvnHome}/bin"]) {
		    sh "mvn clean install -Ddws.environment=jenkins -Dmaven.test.failure.ignore=true -pl '!:dws-acc-tests'"
		}
	}
}

def archiveTestResults() {
	step([$class: 'JUnitResultArchiver', testResults: '**/target/surefire-reports/TEST-*.xml'])
	step([$class: 'JUnitResultArchiver', testResults: '**/target/failsafe-reports/TEST-*.xml'])
}

def archiveArtifacts() {
 	step([$class: 'ArtifactArchiver', artifacts: '**/target/*.jar', fingerprint: true])
 	step([$class: 'ArtifactArchiver', artifacts: '**/dws-ui/target/dist/**/*', fingerprint: true])
}

def stopWhenFailures() {
	if (currentBuild.result != null && !currentBuild.result.equalsIgnoreCase("STABLE")) {
	    error 'There are failures in the current stage. The current build will be stopped.'
	}
}