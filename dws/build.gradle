allprojects {
    group = 'be.cegeka'
    version = '0.0.0-SNAPSHOT'

    repositories {
        jcenter()
    }

    ext {
        versions = [
                spring_boot : '1.3.1.RELEASE',
                javax_inject: '1',
                assertj     : '3.2.0',
                jdbi        : '2.70',
                postgresql  : '9.4-1206-jdbc41',
                node        : '5.3.0',
                npm         : '3.5.2'
        ]
    }
}

subprojects {
    buildscript {
        repositories {
            jcenter()
            maven { url "https://plugins.gradle.org/m2/" }
            maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
            maven { url "http://repo.spring.io/release" }
            maven { url "http://repo.spring.io/milestone" }
            maven { url "http://repo.spring.io/snapshot" }
        }
        dependencies {
            classpath group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
            classpath 'com.wiredforcode:gradle-spawn-plugin:0.6.0'
            classpath("org.springframework.boot:spring-boot-gradle-plugin:${versions.spring_boot}")
        }
    }

    apply plugin: 'java'
    apply from: "$rootDir/gradle/addTestJarArchive.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task packageSources(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource

    }
    artifacts.archives packageSources

    dependencies {
        compile group: 'javax.inject', name: 'javax.inject', version: versions.javax_inject
        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.spring_boot) {
            exclude(module: 'commons-logging')
        }
        testCompile group: 'org.assertj', name: 'assertj-core', version: versions.assertj
    }
}
